# - Try to find KMSCORE library

#=============================================================================
# Copyright 2014 Kurento
#
#=============================================================================

set(PACKAGE_VERSION "@PROJECT_VERSION@")
set(KMSCORE_VERSION ${PACKAGE_VERSION})

message (STATUS "Looking for KMSCORE: @PROJECT_VERSION@")

include (GenericFind)

set (REQUIRED_VARS
  KMSCORE_VERSION
  KMSCORE_INCLUDE_DIRS
  KMSCORE_LIBRARY
  KMSCORE_LIBRARIES
)

set (KMSCORE_BINARY_DIR_PREFIX "build" CACHE PATH "Path prefix used to look for binary files")
set (KMSCORE_SOURCE_DIR_PREFIX "" CACHE PATH "Path prefix used to look for source files")

set(KMSCORE_INCLUDE_DIRS
)

if (NOT "@_INTERFACE_GENERATED_HEADERS@ @_INTERFACE_INTERNAL_GENERATED_HEADERS@" STREQUAL " ")
  if (TARGET kmscoreinterface)
    set (KMSCORE_INTERFACE_INCLUDE_DIR "@_INTERFACE_HEADERS_DIR@")
  else ()
    find_path(KMSCORE_INTERFACE_INCLUDE_DIR
      NAMES
        @_INTERFACE_GENERATED_HEADERS@
        @_INTERFACE_INTERNAL_GENERATED_HEADERS@
      PATH_SUFFIXES
        @_INTERFACE_HEADERS_DIR@
        kurento/modules/core
    )
  endif ()

  list (APPEND KMSCORE_INCLUDE_DIRS ${KMSCORE_INTERFACE_INCLUDE_DIR})
  list (APPEND REQUIRED_VARS KMSCORE_INTERFACE_INCLUDE_DIR)
endif ()

if (NOT "@_SERVER_INTERNAL_GENERATED_HEADERS@" STREQUAL "")
  if (TARGET kmscoreimpl)
    set (KMSCORE_IMPLEMENTATION_INTERNAL_INCLUDE_DIR "@_SERVER_INTERNAL_GENERATED_HEADERS_DIR@")
  else ()
    find_path(KMSCORE_IMPLEMENTATION_INTERNAL_INCLUDE_DIR
      NAMES
        @_SERVER_INTERNAL_GENERATED_HEADERS@
      PATH_SUFFIXES
        @_SERVER_INTERNAL_GENERATED_HEADERS_DIR@
        kurento/modules/core
    )
  endif ()

  list (APPEND KMSCORE_INCLUDE_DIRS ${KMSCORE_IMPLEMENTATION_INTERNAL_INCLUDE_DIR})
  list (APPEND REQUIRED_VARS KMSCORE_IMPLEMENTATION_INTERNAL_INCLUDE_DIR)
endif ()

if (NOT "@_SERVER_GENERATED_HEADERS@" STREQUAL "")
  if (TARGET kmscoreimpl)
    set (KMSCORE_IMPLEMENTATION_GENERATED_INCLUDE_DIR "${KMSCORE_SOURCE_DIR_PREFIX}/@_PARAM_SERVER_STUB_DESTINATION@")
  else ()
    find_path(KMSCORE_IMPLEMENTATION_GENERATED_INCLUDE_DIR
      NAMES
        @_SERVER_GENERATED_HEADERS@
      PATH_SUFFIXES
        @_PARAM_SERVER_STUB_DESTINATION@
        kurento/modules/core
    )
  endif ()

  list (APPEND KMSCORE_INCLUDE_DIRS ${KMSCORE_IMPLEMENTATION_GENERATED_INCLUDE_DIR})
  list (APPEND REQUIRED_VARS KMSCORE_IMPLEMENTATION_GENERATED_INCLUDE_DIR)
endif()

if (NOT "@_PARAM_SERVER_IMPL_LIB_EXTRA_HEADERS@" STREQUAL "")
  if (TARGET kmscoreimpl)
    set (KMSCORE_IMPLEMENTATION_EXTRA_INCLUDE_DIR "${KMSCORE_SOURCE_DIR_PREFIX}/@_PARAM_SERVER_IMPL_LIB_EXTRA_HEADERS_PREFIX@")
  else ()
    find_path(KMSCORE_IMPLEMENTATION_EXTRA_INCLUDE_DIR
      NAMES
        @_PARAM_SERVER_IMPL_LIB_EXTRA_HEADERS@
      PATH_SUFFIXES
        @_PARAM_SERVER_IMPL_LIB_EXTRA_HEADERS_PREFIX@
        kurento/modules/core
    )
  endif ()

  list (APPEND KMSCORE_INCLUDE_DIRS ${KMSCORE_IMPLEMENTATION_EXTRA_INCLUDE_DIR})
  list (APPEND REQUIRED_VARS KMSCORE_IMPLEMENTATION_EXTRA_INCLUDE_DIR)
endif ()

if (NOT "@_PARAM_INTERFACE_LIB_EXTRA_HEADERS@" STREQUAL "")
  if (TARGET kmscoreinterface)
    set (KMSCORE_INTERFACE_EXTRA_INCLUDE_DIR "${KMSCORE_SOURCE_DIR_PREFIX}/@_PARAM_INTERFACE_LIB_EXTRA_HEADERS_PREFIX@")
  else ()
    find_path(KMSCORE_INTERFACE_EXTRA_INCLUDE_DIR
      NAMES
        @_PARAM_INTERFACE_LIB_EXTRA_HEADERS@
      PATH_SUFFIXES
        @_PARAM_INTERFACE_LIB_EXTRA_HEADERS_PREFIX@
        kurento/modules/core
    )
  endif()

  list (APPEND KMSCORE_INCLUDE_DIRS ${KMSCORE_INTERFACE_EXTRA_INCLUDE_DIR})
  list (APPEND REQUIRED_VARS KMSCORE_INTERFACE_EXTRA_INCLUDE_DIR)
endif ()

if (TARGET kmscoreimpl)
  set (KMSCORE_LIBRARY kmscoreimpl)
else ()
  find_library (KMSCORE_LIBRARY
    NAMES
      kmscoreimpl
    PATH_SUFFIXES
      ${KMSCORE_BINARY_DIR_PREFIX}/src/server
  )
endif()

set (REQUIRED_LIBS "@REQUIRED_LIBS@")
foreach (LIB ${REQUIRED_LIBS})
  string(FIND ${LIB} " " POS)

  if (${POS} GREATER 0)
    string(REPLACE " " ";" REQUIRED_LIB_LIST ${LIB})
    include (CMakeParseArguments)
    cmake_parse_arguments("PARAM" "" "LIBNAME" "" ${REQUIRED_LIB_LIST})

    if (DEFINED PARAM_LIBNAME)
      generic_find (${REQUIRED_LIB_LIST} REQUIRED)
      set (LIB_NAME ${PARAM_LIBNAME})
    else()
      string (SUBSTRING ${LIB} 0 ${POS} LIB_NAME)
      string (SUBSTRING ${LIB} ${POS} -1 LIB_VERSION)
      string (STRIP ${LIB_NAME} LIB_NAME)
      string (STRIP ${LIB_VERSION} LIB_VERSION)
      generic_find (LIBNAME ${LIB_NAME} REQUIRED VERSION "${LIB_VERSION}")
    endif()
  else ()
    string (STRIP ${LIB} LIB_NAME)
    generic_find (LIBNAME ${LIB_NAME} REQUIRED)
  endif ()
  list (APPEND REQUIRED_LIBRARIES ${${LIB_NAME}_LIBRARIES})
  list (APPEND KMSCORE_INCLUDE_DIRS ${${LIB_NAME}_INCLUDE_DIRS})

endforeach()

set(KMSCORE_INCLUDE_DIRS
  ${KMSCORE_INCLUDE_DIRS}
  CACHE INTERNAL "Include directories for KMSCORE library" FORCE
)

set (KMSCORE_LIBRARIES
  ${KMSCORE_LIBRARY}
  ${REQUIRED_LIBRARIES}
  CACHE INTERNAL "Libraries for KMSCORE" FORCE
)

include (FindPackageHandleStandardArgs)

find_package_handle_standard_args(KMSCORE
  FOUND_VAR
    KMSCORE_FOUND
  REQUIRED_VARS
    ${REQUIRED_VARS}
  VERSION_VAR
    KMSCORE_VERSION
)

mark_as_advanced(
  KMSCORE_FOUND
  KMSCORE_VERSION
  KMSCORE_INTERFACE_INCLUDE_DIR
  KMSCORE_IMPLEMENTATION_INTERNAL_INCLUDE_DIR
  KMSCORE_IMPLEMENTATION_GENERATED_INCLUDE_DIR
  KMSCORE_IMPLEMENTATION_EXTRA_INCLUDE_DIR
  KMSCORE_INTERFACE_EXTRA_INCLUDE_DIR
  KMSCORE_INCLUDE_DIRS
  KMSCORE_LIBRARY
  KMSCORE_LIBRARIES
)
